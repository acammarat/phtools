!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! 
!                                                                             
! qpoints. Copyright (C) Antonio Cammarata 
! https://nano.cvut.cz/researchers/antonio-cammarata 
! https://orcid.org/0000-0002-5691-0682              
!                                       
! Extracts phonon eigenvectors and eigenvalues from 
! the file qpoints.yaml generated by PHONOPY 
! ( https://phonopy.github.io/phonopy )        
!                                       
! If used for production, you should cite 
! Phys. Rev. B XX, XXXXX (XXXX)    
! https://doi.org/10.1103/xxx 
!                                                                    
!    This file is part of qpoints.                                        
!                                                                      
!    qpoints is free software: you can redistribute it and/or modify 
!    it under the terms of the GNU General Public License as published by 
!    the Free Software Foundation, either version 3 of the License, or 
!    (at your option) any later version.                            
!                                                                 
!    qpoints is distributed in the hope that it will be useful, 
!    but WITHOUT ANY WARRANTY; without even the implied warranty of 
!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the      
!    GNU General Public License for more details.                     
! 
!    You should have received a copy of the GNU General Public License        
!    along with phonchar.  If not, see <http://www.gnu.org/licenses/>. 
!                 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! 

subroutine init
  use var, only: npoints, side_eq_RC, pi2, q, &
                 nskip, skipmod
  use functions, only: i2a, cross
  use, intrinsic :: iso_fortran_env, only: output_unit
  use String_mod, only: String_type

  implicit none
  integer :: i
  integer :: sa, sb, sc
  real(8) :: side_eq_UC(3,3), tmp_a(3), tmp_b(3)
  real(8) :: vol
  character(256) :: infile, poscar, line
  type(String_type) :: phonopy
  logical :: file_exists

  call show_logo

  call get_command_argument(1,infile)
  if ( (infile == '-h') .or. (infile == '') ) then
    write(*,'(a)') ' Syntax: qpoints <setting file>'
    write(*,*)
    stop
  end if

  inquire(file=infile,exist=file_exists)
  if ( .not. (file_exists) ) then
    write(*,'(*(a))') ' ERROR: input file ',trim(infile),' not found.'
    write(*,*)
    stop
  end if

  write(*,*) 'Initializing...'
  open(unit=10,file=infile,action='READ')
  write(*,'(*(a))') '  Reading settings from file: ', trim(infile)

  read(10,'(a)') line
  phonopy%value = trim(line)

  read(10,*) poscar
  inquire(file=poscar,exist=file_exists)
  if ( .not. (file_exists) ) then
    write(*,'(*(a))') '  ERROR: POSCAR file ',trim(poscar),' not found.'
    write(*,*)
    stop
  end if
  write(*,'(*(a))') '  Reading reference structure from file: ', trim(poscar)

  read(10,*) sa, sb, sc
  write(*,'(*(a))') '  FORCE_CONSTANTS created from supercell size: ', i2a(sa),' ',i2a(sb),' ',i2a(sc)

  read(10,*) nskip
  if ( nskip<0 ) then
    write(*,'(a)') '  ERROR: the number of modes to skip must be greater or equal to zero'
    write(*,*)
    stop
  end if
  write(*,'(*(a))') '  Number of modes to skip: ',i2a(nskip)
  write(*,'(a)') '    Acoustic and rotational modes should be listed among the skip modes, if any'

  allocate( skipmod(nskip,2), stat = i )
  if ( i /= 0 ) stop 'Allocation failed for skipmod'

  read(10,*) (skipmod(i,1), skipmod(i,2),i=1,nskip)

  read(10,*) npoints
  write(*,'(*(a))') '  Number of q-points: ', i2a(npoints)

  allocate( q(npoints,3), stat = i )
  if ( i /= 0 ) stop 'Allocation failed for Q'

  do i = 1, npoints
    read(10,*) q(i,:)
  end do
  close(10)

  open(unit=15,file=poscar,action='READ') 
  read(15,*)
  read(15,*)
  do i = 1, 3
    read(15,*) side_eq_UC(i,:)
  end do
  close(15)

  tmp_a(:) = side_eq_UC(2,:)
  tmp_b(:) = side_eq_UC(3,:)

  vol=dot_product( side_eq_UC(1,:), cross( tmp_a ,tmp_b ) )

  side_eq_RC(1,:) = pi2/vol * cross( tmp_a ,tmp_b )
  tmp_a(:) = side_eq_UC(3,:)
  tmp_b(:) = side_eq_UC(1,:)
  side_eq_RC(2,:) = pi2/vol * cross( tmp_a ,tmp_b )
  tmp_a(:) = side_eq_UC(1,:)
  tmp_b(:) = side_eq_UC(2,:)
  side_eq_RC(3,:) = pi2/vol * cross( tmp_a ,tmp_b )

  open(unit=15,file='QPOINTS',action='WRITE')
  write(15,*) npoints
  do i = 1, npoints
    write(15,'(3f20.15)') q(i,:)
  end do
  close(15)
  write(*,'(a37)') '  List of q-points written in QPOINTS'
  
  open(unit=15,file='qpoints.conf',action='WRITE')
  write(15,'(a)') 'FC_SYMMETRY = .TRUE.'
  write(15,'(a)') 'FORCE_CONSTANTS = READ'
  write(15,'(a)') 'EIGENVECTORS = .TRUE.'
  write(15,'(*(a))') 'DIM = ', i2a(sa), ' ', i2a(sb), ' ', i2a(sc)
  write(15,'(a)') 'WRITEDM = .FALSE.'
  write(15,'(a)') 'QPOINTS = .TRUE.'
  close(15)
  write(*,'(a)') '  Phonopy input written in qpoints.conf'

  phonopy%Parts = phonopy%split(phonopy%value, delim = "#")
  write(infile,'(*(a))') trim(phonopy%Parts(1)%record), ' qpoints.conf > phonopy.out'
  write(*,'(*(a))') '  Calling phonopy with command: ',trim(infile)
  call system(infile, i)
  if ( i /= 0 ) then
    write(*,'(*(a))') '  ERROR: phonopy exited with status ', i2a(i)
    deallocate ( q, stat = i )
    if ( i /= 0 ) stop 'Deallocation failed for q'
    stop
  end if
  write(*,'(a)') ' Initialization done. File qpoints.yaml created.'

 
  return
end subroutine init


